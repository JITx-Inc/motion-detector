#use-added-syntax(jitx)
defpackage components/antenna :
  import core
  import jitx
  import jitx/commands
  import jitx/parts
  import ocdb/utils/checks

  import ocdb/utils/generator-utils
  import ocdb/utils/landpatterns
  import ocdb/utils/symbols

  import helpers
  import board
  import jsl/si

; ========================================================
; ===================== Antennas =========================
; ========================================================

pcb-landpattern antenna-2GHz4-inverted-f-geometry :
  pad launch : smd-pad(0.5, 1.5) at loc(0.0, 0.55)
  pad gnd : smd-pad(0.9, 0.5) at loc(-2.1, 0.25)

  layer(SolderMask(Top)) = Rectangle(20.0, 5.5, loc(5.0, 3.25))
  layer(Courtyard(Top)) = Rectangle(20.0, 5.5, loc(5.0, 3.25))
  layer(Courtyard(Bottom)) = Rectangle(20.0, 5.5, loc(5.0, 3.25))
  layer(CustomLayer("Fab", Top)) = Line(0.01, [Point(15.0, 6.0), Point(-5.0, 6.0)])
  layer(ForbidCopper(LayerIndex(0), LayerIndex(0, Bottom))) = Rectangle(20.0, 5.5, loc(5.0, 3.25))

  ref-label()

public pcb-component inverted-f-antenna-component :
  port launch
  port gnd
  description = "2.4GHz Inverted-F printed antenna"
  val pkg = antenna-2GHz4-inverted-f-geometry
  val sym = antenna-symbol(1, 1)
  landpattern = pkg(launch => pkg.launch, gnd => pkg.gnd)
  symbol = sym(launch => sym.p[1], gnd => sym.p[2])
  reference-prefix = "ANT"
  property(self.rated-temperature) = false

public pcb-module antenna-2GHz4-inverted-f-module :
  port launch
  port gnd

  public inst antenna : components/antenna/inverted-f-antenna-component
  place(antenna) at loc(0.0, 0.0) on Top

  net a ()
  topo-net(launch antenna.launch)
  net (a launch)
  net (gnd antenna.gnd)
  ; net (a gnd) ; to avoid DRC errors?

  geom(a) :
    copper(LayerIndex(0)) = Line(0.5, [Point(-2.1, 5.15), Point(2.2, 5.15), Point(2.2, 2.51),
                                  Point(4.7, 2.51),  Point(4.7, 5.15), Point(6.9, 5.15),
                                  Point(6.9, 2.51),  Point(9.4, 2.51), Point(9.4, 5.15),
                                  Point(11.6, 5.15), Point(11.6, 0.96)])
    copper(LayerIndex(0)) = Rectangle(0.5, 4.10 loc(0., 3.325))
    copper(LayerIndex(0)) = Rectangle(0.9, 4.9, loc(-2.1, 2.95))

  instance-status(antenna) :
    bom-status = NotInBOM

public pcb-module pi-filter (-- spacing:Double = 1.0):
  port vin
  port vout
  port gnd

  inst C-F : create-capacitor(CapacitorQuery(c0g-cap-query, capacitance = 0.8e-12))
  inst C-R : create-capacitor(CapacitorQuery(c0g-cap-query, capacitance = 0.5e-12))
  inst L   : create-inductor(InductorQuery(ind-query, inductance = 4.7e-9 self-resonant-frequency = 4.0e9 quality-factor = 8.0))

  topo-net(vin => C-F.p[1] => L.p[1])
  pin-model(L.p[1] L.p[2]) = PinModel(typ(0.0), typ(0.0))
  topo-net(L.p[2] => C-R.p[1] => vout)
  net (gnd C-F.p[2] C-R.p[2])

  schematic-group(self) = PI-FILTER
  place(C-F) at loc( 1.200 * spacing, 0.600, 0.0) on Top
  place(C-R) at loc(-1.200 * spacing, 0.600, 0.0) on Top
  place(L) at loc(0.000, 1.00, 270.00) on Top

public pcb-module inverted-f-antenna :
  port rf-in
  port gnd

  public inst ant : antenna-2GHz4-inverted-f-module

  place(ant) at loc(0.0, 0.0, 0.0) on Top
  ; PI filter
  public inst pi : pi-filter(spacing = 1.0)

  topo-net(rf-in pi.vin)
  topo-net(pi.vout ant.launch)
  set-signal-end(rf-in, ant.launch)
  net GND (gnd, ant.gnd pi.gnd)

  schematic-group(self) = inverted-f-antenna

  ; add some via stitching next to the antenna
  defn not-skip-index (a) -> True|False :
    a < 4 or a > 5 and a < 7 or a > 11

  val y = -0.1
  for x in 0 to 19 do :
    if not-skip-index(x) :
      val x-d = to-double(x)
      geom(GND) :
        via(small-via) at Point(x-d - 4.4, y)

public pcb-module test-RF-conn :
  port sig
  port gnd

  ; We add a u.FL connector with a separate inductor and capacitor that are
  ;   generally DNP. This will be used for FCC verification.
  inst C-R : create-capacitor(CapacitorQuery(c0g-cap-query, capacitance = 0.5e-12))
  inst L   : create-inductor(InductorQuery(ind-query, inductance = 4.7e-9 self-resonant-frequency = 4.0e9 quality-factor = 8.0))

  inst test-uFL : components/HRS/U-FL-R-SMT-10-/module

  topo-net(sig => L.p[1])
  pin-model(L.p[1] L.p[2]) = PinModel(typ(0.0), typ(0.0))
  topo-net(L.p[2] => C-R.p[1] => test-uFL.sig)
  set-signal-end(sig, test-uFL.sig)

  net (gnd, C-R.p[2], test-uFL.gnd)

  for i in [C-R, L, test-uFL] do :
    instance-status(i) :
      bom-status = NotInBOM
